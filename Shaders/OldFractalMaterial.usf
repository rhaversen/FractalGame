// Post-process Mandelbulb rendering shader functions for use in material custom node

#define FRACTAL_MARCH_MAX_STEPS 150
#define FRACTAL_MARCH_MAX_DISTANCE 500.0
#define FRACTAL_DE_MAX_ITER 150  // Safety cap - adaptive iteration will stop earlier when stable
#define FRACTAL_DE_BAILOUT 10.0
#define FRACTAL_DE_MIN_ITERATIONS 5  // Always do at least this many iterations for stability
#define FRACTAL_DE_CONVERGENCE_FACTOR 0.01  // Stop when DE change is 1% of required precision

#define HIT_STATUS_NONE 0
#define HIT_STATUS_HIT 1
#define HIT_STATUS_MISS_DISTANCE 2
#define HIT_STATUS_MISS_STEPS 3

struct MarchResult
{
	float distance;
	int steps;
	int hitStatus;
	int totalDEIterations;
};

struct DEResult
{
	float distance;
	int iterations;
};

struct SphericalCoords
{
	float r;
	float theta;
	float phi;
};

struct FractalFunctions
{
	// Convert Cartesian coordinates to spherical
	static SphericalCoords CartesianToSpherical(float3 z)
	{
		SphericalCoords result;
		result.r = length(z);
		float invR = (result.r > 0.0) ? 1.0 / result.r : 0.0;
		float cosTheta = clamp(z.z * invR, -1.0, 1.0);
		result.theta = (result.r > 0.0) ? acos(cosTheta) : 0.0;
		result.phi = atan2(z.y, z.x);
		return result;
	}

	// Convert spherical coordinates back to Cartesian
	static float3 SphericalToCartesian(float r, float theta, float phi)
	{
		float sinTheta = sin(theta);
		return r * float3(sinTheta * cos(phi), sinTheta * sin(phi), cos(theta));
	}

	// Apply power rotation in spherical domain
	static void RotateSpherical(inout float theta, inout float phi, float power)
	{
		theta *= power;
		phi *= power;
	}

	static DEResult MakeDEResult(float radius, float derivative, int iterations)
	{
		float safeRadius = max(radius, 1e-6);
		float safeDerivative = max(abs(derivative), 1e-10);

		DEResult result;
		result.distance = 0.5 * log(safeRadius) * safeRadius / safeDerivative;
		result.iterations = iterations;
		return result;
	}

	// Computes the world-space radius that a sphere must have to appear 1 pixel tall on screen at a given distance.
	static float GetPixelWorldRadius(float distance)
	{
		// This is derived from the projection formula: pixel_size = (world_radius / distance) * screen_height / tan(fov/2)
		// Solving for world_radius when pixel_size = 1 gives:
		return distance * ResolvedView.ClipToView._22 / ResolvedView.ViewSizeAndInvSize.y;
	}

	// ============================================================================
	// POWER FRACTAL CORE - Mandelbulb distance estimation
	// ============================================================================

	static float3 SphericalPowerTransform(float3 z, float power)
	{
		SphericalCoords coords = CartesianToSpherical(z);
		float zr = pow(coords.r, power);
		RotateSpherical(coords.theta, coords.phi, power);
		return SphericalToCartesian(zr, coords.theta, coords.phi);
	}

	static float3 MandelbulbTransform(float3 z, float3 pos, float power)
	{
		return SphericalPowerTransform(z, power) + pos;
	}

	static DEResult MandelbulbDE(float3 pos, float power, float precisionThreshold)
	{
		float3 z = pos;
		float dr = 1.0;
		float prevDE = 1e10; // Start with large value
		int i;

		for (i = 0; i < FRACTAL_DE_MAX_ITER; i++)
		{
			float r = length(z);
			if (r > FRACTAL_DE_BAILOUT)
				break;

			float currentDE = 0.5 * log(max(r, 1e-6)) * r / max(dr, 1e-10);

			// Check if estimate has converged enough for required precision
			// Only check after minimum iterations to ensure stability
			if (i >= FRACTAL_DE_MIN_ITERATIONS)
			{
				float deChange = abs(currentDE - prevDE);
				if (deChange < precisionThreshold * FRACTAL_DE_CONVERGENCE_FACTOR)
					break; // Estimate is stable enough!
			}

			prevDE = currentDE;

			// Update derivative (guard small radii to keep derivative stable)
			float safeR = max(r, 1e-6);
			dr = pow(safeR, power - 1.0) * power * dr + 1.0;

			// Apply Mandelbulb transformation
			z = MandelbulbTransform(z, pos, power);
		}

		return MakeDEResult(length(z), dr, i);
	}

	// Marching function
	static MarchResult MarchFractal(float3 RayOrigin, float3 RayDir, float power)
	{
		float totalDist = 0.0;
		int steps = 0;
		int totalDEIterations = 0;

		while (totalDist < FRACTAL_MARCH_MAX_DISTANCE && steps < FRACTAL_MARCH_MAX_STEPS)
		{
			steps++;
			float3 pos = RayOrigin + RayDir * totalDist;
			
			// Calculate precision threshold for adaptive DE iterations
			float pixelSize = GetPixelWorldRadius(totalDist);
			
			// Call DE with precision threshold - it will iterate until stable enough
			DEResult deResult = MandelbulbDE(pos, power, pixelSize);
			
			// Accumulate total DE iterations
			totalDEIterations += deResult.iterations;

			float threshold = pixelSize;

			// Check if we've hit the fractal
			if (deResult.distance <= threshold)
			{
				MarchResult result;
				result.distance = totalDist;
				result.steps = steps;
				result.hitStatus = HIT_STATUS_HIT;
				result.totalDEIterations = totalDEIterations;
				return result;
			}

			// Advance by the distance estimate, with minimum step to avoid precision issues
			float stepSize = max(deResult.distance, pixelSize * 0.5); // Never step smaller than half a pixel
			totalDist += stepSize;
		}

		MarchResult result;
		result.distance = totalDist;
		result.steps = steps;
		result.hitStatus = (totalDist >= FRACTAL_MARCH_MAX_DISTANCE) ? HIT_STATUS_MISS_DISTANCE : HIT_STATUS_MISS_STEPS;
		result.totalDEIterations = totalDEIterations;
		return result;
	}

	static float3 RenderFractal(float2 screenUV, float3 sceneColor, float power, float scaleMultiplier)
	{
		// Get camera parameters
		float3 cameraOrigin = LWCHackToFloat(ResolvedView.WorldCameraOrigin);

		// Convert screen UV to clip space
		float2 clipXY = screenUV * 2.0 - 1.0;
		clipXY.y = -clipXY.y; // Flip Y for clip space

		float4 clipPos = float4(clipXY, 1.0, 1.0);
		float4 viewPos = mul(clipPos, ResolvedView.ClipToView);
		viewPos.xyz /= viewPos.w;

		// Transform view direction to world space
		float3 worldDir = normalize(mul(viewPos.xyz, (float3x3)ResolvedView.ViewToTranslatedWorld));

		// Scale the camera origin by the scale multiplier
		float3 rayOrigin = cameraOrigin * scaleMultiplier;
		float3 rayDir = worldDir;

		// Ray march
		MarchResult finalResult = MarchFractal(rayOrigin, rayDir, power);

		// Calculate color based on total steps taken
		float stepFactor = saturate(finalResult.steps / float(FRACTAL_MARCH_MAX_STEPS));
		float t = sqrt(stepFactor);
		
		// Gradient color stops: position -> color
		float  almostBlackPos = 0.0;   float3 almostBlack = float3(0.0, 0.0, 0.05);    // Very few steps
		float  deepBluePos = 0.15;     float3 deepBlue = float3(0.05, 0.15, 0.3);      // Low detail areas
		float  vibrantBluePos = 0.4;   float3 vibrantBlue = float3(0.2, 0.4, 0.8);     // Medium detail
		float  brightBluePos = 1.0;    float3 brightBlue = float3(0.6, 0.8, 1.0);      // Many steps
		
		// Blend through gradient stops
		float range1 = saturate((t - almostBlackPos) / (deepBluePos - almostBlackPos));
		float range2 = saturate((t - deepBluePos) / (vibrantBluePos - deepBluePos));
		float range3 = saturate((t - vibrantBluePos) / (brightBluePos - vibrantBluePos));
		float3 fractalColor = lerp(lerp(lerp(almostBlack, deepBlue, range1), vibrantBlue, range2), brightBlue, range3);

		// Add green tint based on total DE iterations
		float iterFactor = saturate(finalResult.totalDEIterations / float(FRACTAL_DE_MAX_ITER * finalResult.steps));
		float greenThreshold = 0.1;
		if (iterFactor > greenThreshold)
		{
			// Steep exponential curve from greenThreshold to 1.0
			float greenAmount = (iterFactor - greenThreshold) / (1.0 - greenThreshold);
			greenAmount = pow(greenAmount, 2.0); // Quadratic for steep ramp
			fractalColor = lerp(fractalColor, float3(0.3, 0.5, 0.6), greenAmount);
		}

		float greenPeak = 0.9;
		if (iterFactor > greenPeak)
		{
			// Steep exponential curve from greenPeak to 1.0
			float greenAmount = (iterFactor - greenPeak) / (1.0 - greenPeak);
			greenAmount = pow(greenAmount, 5.0); // Quadratic for steep ramp
			fractalColor = lerp(fractalColor, float3(0.3, 0.9, 0.7), greenAmount);
		}

		if (finalResult.hitStatus == HIT_STATUS_HIT)
		{
			// Direct hit: show fractal color
			return fractalColor;
		}
		else if (finalResult.hitStatus == HIT_STATUS_MISS_DISTANCE)
		{
			// Missed due to distance: blend fractal color with scene color based on steps taken
			float fogAmount = pow(stepFactor, 0.1);
			return lerp(sceneColor, fractalColor, fogAmount);
		}
		else if (finalResult.hitStatus == HIT_STATUS_MISS_STEPS)
		{
			// Missed due to max steps: show fractal color (TODO: Find a better way to handle this case, use depth?)
			return fractalColor;
		}
		else
		{
			// Should not happen, fallback to scene color
			return sceneColor;
		}
	}

};

// Usage in material custom node:
// #include "/Project/FractalMaterial.usf"
// FractalFunctions FF;
// return FF.RenderFractal(ScreenPosition, SceneColor, Power, ScaleMultiplier).rgb;
//
// Parameters:
// - Power: Float power parameter (typically 1.0-19.0)
// - ScaleMultiplier: Float world scale (0.0001-0.01, default 0.001) - controls zoom level/world size